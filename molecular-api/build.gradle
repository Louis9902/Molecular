apply from: "${scripts}/java_base.gradle"

dependencies {
    compile group: 'io.netty', name: 'netty-all', version: '4.1.15.Final'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.24'
    compile group: 'com.google.guava', name: 'guava', version: '23.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
}

task processDependencies(type: Copy) {
    into "${project.buildDir}/dependencies"
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

task jarSources(type: Jar, dependsOn: classes, group: 'build') {
    classifier = 'source'
    from sourceSets.main.allSource
}

task jarJavadoc(type: Jar, dependsOn: javadoc, group: 'build') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task jarAll(type: Jar, dependsOn: [compileJava, processDependencies, processResources], group: 'build') {
    classifier = 'all'
    from compileJava.destinationDir
    from processDependencies.destinationDir
    from processResources.destinationDir
}

task zipArtifacts(type: Zip, dependsOn: [jar, jarJavadoc, jarSources], group: 'artifacts') {
    classifier = 'artifacts'
    from jarAll
    from jarJavadoc
    from jarSources
    from file("${rootDir}/LICENCE")
    from file("${rootDir}/README")
}

artifacts {
    jar
    jarAll
    jarJavadoc
    jarSources
}